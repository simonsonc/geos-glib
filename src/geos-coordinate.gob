%privateheader{
#include <geos/geom/Coordinate.h>
%}

%alltop{
#include "geos-point.h"
%}

%{
#include "geos-point-private.h"
%}

%header{
G_BEGIN_DECLS
%}

class Geos:Coordinate from G:Object {
    private const geos::geom::Coordinate* hndl;

    public Geos:Coordinate* new (void) {
        auto self = GET_NEW;
        self->_priv->hndl = new geos::geom::Coordinate();
        return self;
    }

    public Geos:Coordinate* new_with_xy (gdouble x, gdouble y) {
        auto self = GET_NEW;
        self->_priv->hndl = new geos::geom::Coordinate(x, y);
        return self;
    }

    property DOUBLE x
        get { g_value_set_double(VAL, self->_priv->hndl->x); };
    property DOUBLE y
        get { g_value_set_double(VAL, self->_priv->hndl->y); };
    property DOUBLE z
        get { g_value_set_double(VAL, self->_priv->hndl->z); };

    public gchar* to_string(self) {
        return g_strdup(self->_priv->hndl->toString().c_str());
    }

    public gboolean is_null(self) {
        return self->_priv->hndl->isNull();
    }
}

%header{
G_END_DECLS
%}
